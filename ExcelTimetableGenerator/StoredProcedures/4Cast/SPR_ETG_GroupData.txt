CREATE PROCEDURE [dbo].[SPR_ETG_GroupData]
	@AcademicYear NVARCHAR(5),
	@PlanRevisionID INT,
	@Course NVARCHAR(50)
AS
BEGIN
	SET NOCOUNT ON;

	--DECLARE @Course NVARCHAR(50) = 'BUSL522139'

	SELECT
		GroupID = CAST ( PRG.ProgID AS NVARCHAR(20) ) + N'-' + CAST ( GRP.GroupNum AS NVARCHAR(10) ),
		ProgrammeID = COALESCE ( PRG.ProgID, 0 ),
		GroupCode = GRP.GroupCode,
		ProgCodeWithGroup = PRG.ProgCode + '-' + GRP.GroupCode
	FROM (
		SELECT
			ProgID = COALESCE ( FC.ProgID, 0 ),
			ProgCode = FC.ProgCode,
			NumGroups = MAX ( COALESCE ( TRY_CAST ( CASE WHEN FC.IsMainAim = 1 THEN FC.UDF22 ELSE 1 END AS INT ), 1 ) ) --Only consider courses that are main aims in this.
		FROM [WLCBA-FCST001A].wlc.dbo.VW_4CA_4Cast2021Data FC
		WHERE
			FC.ProgCode = COALESCE ( @Course, FC.ProgCode )
		GROUP BY
			COALESCE ( FC.ProgID, 0 ),
			FC.ProgCode
	) PRG
	INNER JOIN (
		SELECT
			GroupNum = CNT.RowNum,
			GroupCode = CHAR ( CNT.RowNum + 64 )
		FROM (
			SELECT
				RowNum = 
					ROW_NUMBER () OVER ( 
						ORDER BY
							OBJ.object_id
					)
			FROM sys.all_objects OBJ
		) CNT
	) GRP
		ON GRP.GroupNum <= COALESCE ( CAST ( CASE WHEN PRG.NumGroups < 1 THEN 1 ELSE PRG.NumGroups END AS INT ), 1 )

	ORDER BY
		PRG.ProgCode,
		GRP.GroupCode
END