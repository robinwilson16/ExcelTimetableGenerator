CREATE PROCEDURE [dbo].[SPR_ETG_ProgrammeData]
	@AcademicYear NVARCHAR(5),
	@PlanRevisionID INT,
	@Course NVARCHAR(50)
AS
BEGIN
	SET NOCOUNT ON;

	--DECLARE @AcademicYear NVARCHAR(5) = N'19/20'
	--DECLARE @PlanRevisionID INT = 66

	SELECT
		ProgrammeID = PRG.OfferingID,
		SiteCode = STE.Code,
		SiteName = STE.Description,
		FacCode = RTRIM ( PS.Level1Code ),
		FacName = FAC.LevelName,
		TeamCode = RTRIM ( PS.Level2Code ),
		TeamName = PS.LevelName,
		ProgCode = PRG.Code,
		ProgTitle = PRG.Name,
		ModeOfAttendanceCode = PRG.ModeOfAttendanceID,
		ModeOfAttendanceName = MOA.Description,
		ProgStatus = COALESCE ( STA.Description, 'Not Assigned' ),
		PLH1618 = PRG.PlannedLearningHours1618,
		PLH19 = PRG.PlannedLearningHours19Plus,
		PLHMax = 
			CASE
				WHEN COALESCE ( PRG.PlannedLearningHours1618, 0 ) > COALESCE ( PRG.PlannedLearningHours19Plus, 0 ) THEN
					COALESCE ( PRG.PlannedLearningHours1618, 0 )
				ELSE
					COALESCE ( PRG.PlannedLearningHours19Plus, 0 )
			END,
		EEP1618 = PRG.PlannedEEPHours1618,
		EEP19 = PRG.PlannedEEPHours19Plus,
		EEPMax = 
			CASE
				WHEN COALESCE ( PRG.PlannedEEPHours1618, 0 ) > COALESCE ( PRG.PlannedEEPHours19Plus, 0 ) THEN
					COALESCE ( PRG.PlannedEEPHours1618, 0 )
				ELSE
					COALESCE ( PRG.PlannedEEPHours19Plus, 0 )
			END
	FROM [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.CCS_Offering PRG --Parent
	INNER JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.PlanEntity PLN
		ON PLN.OfferingID = PRG.OfferingID
		AND PLN.RevisionIdentity = @PlanRevisionID
	INNER JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.StatusCourse STA
		ON STA.StatusCourseID = PLN.StatusCourseID
	LEFT JOIN (
		SELECT DISTINCT --Confirm no parents
			OfferingID = LNK.SubOfferingID
		FROM [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.CCS_LinkedOffering LNK
	) PAR
		ON PAR.OfferingID = PRG.OfferingID
	LEFT JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.Site STE
		ON STE.SiteID = PRG.SiteID
	LEFT JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.ModeOfAttendance MOA
		ON MOA.ModeOfAttendanceID = PRG.ModeOfAttendanceID
	LEFT JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.PlanSummary PS
		ON PS.PlanEntityID = PLN.PlanEntityID
	LEFT JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.CCS_CollegeStructure FAC
		ON FAC.Level1Code = PS.Level1Code
		AND FAC.AcademicYear = @AcademicYear
		AND FAC.LevelNumber = 1
	LEFT JOIN [WLCBA-PROR001M.MIDDLE.MAN].ProResource.dbo.PlanGroupSize PGS
		ON PGS.PlanEntityID = PS.PlanEntityID
	WHERE
		PRG.AcademicYear = @AcademicYear
		AND PRG.Code = COALESCE ( @Course, PRG.Code )
		AND PAR.OfferingID IS NULL --Parent doesn't have a parent
		AND STA.StatusCode = 8 --Running (Approved)
	ORDER BY
		STE.Code,
		PS.Level1Code,
		PS.Level2Code,
		PRG.Code
END